// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the output bits:
 * zr = (out == 0, 1, 0)
 * ng = (out < 0,  1, 0)
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // (out == 0, 1, 0)
        ng;      // (out < 0,  1, 0)

    PARTS:
    Mux16(a = x, b = false, sel = zx, out = ozx);
    Not16(in = ozx, out = nozx);
    Mux16(a = ozx, b = nozx, sel = nx, out = ox);
    Mux16(a = y, b = false, sel = zy, out = ozy);
    Not16(in = ozy , out = nozy);
    Mux16(a = ozy, b = nozy, sel = ny, out = oy);
    Add16(a = ox, b = oy, out = xAndy);
    And16(a = ox, b = oy, out = xPlusy);
    Mux16(a = xPlusy, b = xAndy, sel = f, out = xfy);
    Not16(in = xfy, out = nxfy);
    Mux16(a = xfy, b = nxfy, sel = no, out[0..7] = outbus1 , out[8..15] = outbus2 , out[15] = out15 , out = out);
    Or8Way(in = outbus1, out = or07);
    Or8Way(in = outbus2 , out=or815);
    Or(a = or07 , b = or815 , out = or015);
    Not(in = or015, out = zr);
    Or(a = out15 , b = false , out = ng);
}